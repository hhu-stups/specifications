REFINEMENT
   SingleFlowSeqR1
REFINES SingleFlowSeqSpec
/* In this refinement we add:
   -  a route between source and dest given by Route
   -  every device has a local_queue
   -  packets are no longer consumed directly, they are transmitted from source to destination
      via the route given by Routes
*/
DEFINITIONS scope_PACKETS==1..3
CONSTANTS Route  /* Routes gives for every pair of Devices a route */
PROPERTIES
   Route: iseq1(DEVICES) &
   first(Route)=source & last(Route)=dest
VARIABLES local_queue
INVARIANT
  local_queue: (DEVICES --> iseq(RPACKETS)) &

 /* the one queue between source and destination is now split up into multiple queues along the Route between
    source and destinatin */
  messages = conc((Route ; local_queue))
INITIALISATION
  local_queue :=  %d.(d:DEVICES|<>)
OPERATIONS
  generate_packet(p) = PRE p:PACKETS & p/=emptyp  &
      p/: ran(conc((Route ; local_queue)))
     THEN
      local_queue(source) := p->local_queue(source)
    END;
  transmit_packet(p,i) = PRE p:PACKETS & p/=emptyp & i:NATURAL1 THEN
              SELECT  i:1..(size(Route)-1) & local_queue(Route(i)) /= <> &
                 p= last(local_queue(Route(i))) THEN
                 local_queue := local_queue <+ 
                                { Route(i) |-> front(local_queue(Route(i))),
                                  Route(i+1) |-> (p->local_queue(Route(i+1))) } 
              END
    END;
  r <-- consume_packet =
      SELECT local_queue(dest) /= {} THEN
         r:= last(local_queue(dest)) ||
         local_queue(dest) := front(local_queue(dest))
      END
END